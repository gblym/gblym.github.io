---
layout: post
title: 哦！我的Zsh！
abstract: Zsh（Z Shell）与常见的Bash同为Unix Shell，它提供了极强的可定制性和可扩展性，以及一些有趣的功能。
tags: Shell Zsh Tool
---

不久前，我从[Bash](http://en.wikipedia.org/wiki/Bash_%28Unix_shell%29)切换到了[Zsh](http://en.wikipedia.org/wiki/Z_shell)，写点东西供读者以及我自己参考。

Zsh是什么
---

Zsh是一个Unix Shell，它在兼容标准的POSIX Shell（以及可仿真Bash）的同时，提供了极强的可定制性和可扩展性，以及一些有趣的功能，比如：

* 自定义提示符，可以与git等软件集成；
* 可编程的命令补全，例如输入kill命令后按tab会自动列出进程；
* 全局可共享、并且能以各种方式管理的命令历史；
* 命令补全错误纠正、界面主题包、不输入cd只输入目录名直接进入目录……

Zsh在使用时需要注意的独特之处包括：

* 通配符展开是语言级的，通配符无法匹配是一个语法错误；
* 一些命令是内建的，与其它Shell中的不同，例如echo命令在默认情况下会进行字符转义；
* 从1开始数（第三声）数（第四声）。

> 顺带一提：Zsh取自Zhong Shao的登录名——此人是作者Paul Falstad的大学助教。<del>为了名垂青史，请使用姓名拼音作为登录名，并耐心等待躺枪。</del>我常常混淆Zsh和[ZFS](http://en.wikipedia.org/wiki/ZFS)的各种梗，后者的“Z”代表Zettabyte和“终极”（作为最后一个字母）。

Oh-my-zsh
---

为了充分体验Zsh的强大之处，我们需要对它进行一些配置。

一个简单的方法是使用[Oh-my-zsh](https://github.com/robbyrussell/oh-my-zsh)——Oh-my-zsh是一个Zsh配置管理框架，自带上百个插件和主题。

直接下载并作为Shell脚本运行`http://install.ohmyz.sh`即可安装。

当然，你也可以选择从零开始进行配置。

配置
---

Zsh的配置文件位于`～/.zshrc`。

从文件末尾开始添加配置。首先，我们可以根据自己的喜好，添加一些别名：

{% highlight sh linenos %}
alias ll='ls -al'
alias la='ls -a'
alias apt-get='sudo apt-get'
alias gita='git add'
alias gitc='git commit'
...
{% endhighlight %}

扩展名别名：

{% highlight sh linenos %}
alias -s gz='tar -xzvf'
alias -s bz2='tar -xjvf'
alias -s out='valgrind'
...
{% endhighlight %}

还可以加上快捷地址，比如`hash -d www='/var/www/'`。

Zsh的提示符是可以修改的。我使用的版本：

{% highlight sh linenos %}
ZSH_THEME_GIT_PROMPT_CLEAN=""
ZSH_THEME_GIT_PROMPT_DIRTY=" !"
ZSH_THEME_GIT_PROMPT_PREFIX=" :"
ZSH_THEME_GIT_PROMPT_SUFFIX=""
PROMPT=$(echo "${ret_status}%{$fg_bold[green]%}%p%{$fg[blue]%}%5~\
%{$fg_bold[cyan]%}$(git_prompt_info) %{$reset_color%}")
{% endhighlight %}

一些关于补全和错误纠正的配置：

{% highlight sh linenos %}
zstyle ':completion:*' completer _complete _match _approximate
zstyle ':completion:*:match:*' original only
zstyle ':completion:*:approximate:*' max-errors 1 numeric

zstyle ':completion:*:*:kill:*' menu yes select
zstyle ':completion:*:kill:*' force-list always

zstyle ':completion:*:cd:*' ignore-parents parent pwd
{% endhighlight %}

扩展
---

以下是一些从网上收集的小程序，可以直接在`.zshrc`中使用。

对每个目录使用独立的命令历史：

{% highlight sh linenos %}
mkdir -p $HOME/.zsh_history$PWD
export HISTFILE="$HOME/.zsh_history$PWD/zhistory"

cd() {
    builtin cd "$@"
    fc -W
    local HISTDIR="$HOME/.zsh_history$PWD"
    if  [ ! -d "$HISTDIR" ] ; then
        mkdir -p "$HISTDIR"
    fi
    export HISTFILE="$HISTDIR/zhistory"
    touch $HISTFILE
    local ohistsize=$HISTSIZE
    HISTSIZE=0
    HISTSIZE=$ohistsize
    fc -R
}

function allhistory {
    cat $(find $HOME/.zsh_history -name zhistory)
}

function convhistory {
    sort $1 | uniq |
    sed 's/^:\([ 0-9]*\):[0-9]*;\(.*\)/\1::::::\2/' |
    awk -F"::::::" '{ $1=strftime("%Y-%m-%d %T",$1) "|"; print }' 
}

function histall {
    convhistory =(allhistory) | sed '/^.\{20\} *cd/i\\'
}

function hist {
    convhistory $HISTFILE
}
 
function top50 {
    allhistory | awk -F':[ 0-9]*:[0-9]*;' '{ $1="" ; print }'\
        | sed 's/ /\n/g' | sed '/^$/d' | sort | uniq -c | sort -nr | head -n 50
}
{% endhighlight %}

语法高亮：

{% highlight sh linenos %}
setopt extended_glob
TOKENS_FOLLOWED_BY_COMMANDS=\
    ('|' '||' ';' '&' '&&' 'sudo' 'do' 'time' 'strace' 'man')

recolor-cmd() {
    region_highlight=()
    colorize=true
    start_pos=0
    for arg in ${(z)BUFFER}; do
        ((start_pos+=${#BUFFER[$start_pos+1,-1]}\
            -${#${BUFFER[$start_pos+1,-1]## #}}))
        ((end_pos=$start_pos+${#arg}))
        if $colorize; then
            colorize=false
            res=$(LC_ALL=C builtin type $arg 2>/dev/null)
            case $res in
                *'reserved word'*)   style="fg=magenta,bold";;
                *'alias for'*)       style="fg=cyan,bold";;
                *'shell builtin'*)   style="fg=yellow,bold";;
                *'shell function'*)  style='fg=green,bold';;
                *"$arg is"*)        
                    [[ $arg = 'sudo' ]] && style="fg=red,bold"\
                                  || style="fg=blue,bold";;
                *)                   style='none,bold';;
            esac
            region_highlight+=("$start_pos $end_pos $style")
        fi
        [[ ${${TOKENS_FOLLOWED_BY_COMMANDS[(r)${arg//|/\|}]}:+yes} = 'yes' ]]\
            && colorize=true
        start_pos=$end_pos
    done
}

check-cmd-self-insert() { zle .self-insert && recolor-cmd }
check-cmd-backward-delete-char() { zle .backward-delete-char && recolor-cmd }

zle -N self-insert check-cmd-self-insert
zle -N backward-delete-char check-cmd-backward-delete-char
{% endhighlight %}

按两次esc键，快捷sudo：

{% highlight sh linenos %}
sudo-command-line() {
    [[ -z $BUFFER ]] && zle up-history
    [[ $BUFFER != sudo\ * ]] && BUFFER="sudo $BUFFER"
    zle end-of-line
}
zle -N sudo-command-line
bindkey "\e\e" sudo-command-line
{% endhighlight %}

当然，Zsh还可以变得更酷、更强。通过扩展和配置，我们可以让它做各种事情，尽管发挥你的想象力吧！
